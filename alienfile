use alienfile;

use File::Which qw(which);

my $MAX_VERSION = 15;
for my $command (qw(llvm-ar), ( map "llvm-ar-$_", reverse 12..$MAX_VERSION) ) {
  next unless which($command);
  plugin 'Probe::CommandLine' => (
    command   => $command,
    args      => [ '--version' ],
    version => qr/LLVM version ([0-9\.]+)/,
  );
}

sub do_source {
  plugin 'Download::GitHub' => (
    github_user => 'llvm',
    github_repo => 'llvm-project',
  );
  plugin 'Build::CMake';
  build [
    ['%{cmake}', @{ meta->prop->{plugin_build_cmake}->{args} }, '%{.install.extract}'],
    '%{make}',
    '%{make} install',
  ];
  plugin 'Gather::IsolateDynamic';
  after gather => sub {
    my ($build) = @_;
    $build->runtime_prop->{'style'} = 'source';
  };
}

share {
  do_source;
}
